buildscript {
    repositories {
        [
                '../repo'
        ].each { repo ->
            maven { url repo }
        }
    }

    dependencies {
        classpath group: 'com.github.marcpoppleton', name: 'git-changelog', version: '0.2.0'
    }
}
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'changelog'

group = 'com.github.marcpoppleton'
version = '0.2.0'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Changelog plugin',
                'Implementation-Version': version,
                'Built-By': 'Marc Poppleton',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Changelog plugin')
                root.appendNode('description', 'Gradle plugin for generating changelog from commit logs.\n Forked from Andres Araujos plugin')
                root.appendNode('inceptionYear', '2015')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def marcpoppleton = developers.appendNode('developer')
                marcpoppleton.appendNode('id', 'marcpoppleton')
                marcpoppleton.appendNode('name', 'Marc Poppleton')
                marcpoppleton.appendNode('email', 'marc.poppleton@gmail.com')
            }
        }
        repositories {
            maven {
                url "../repo"
            }

        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gradle Changelog plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Gradle plugin for generating changelog from commit logs. Forked from Andres Araujos plugin'
                url 'https://github.com/marcpoppleton/gradle-changelog-plugin'

                scm {
                    connection 'scm:git:https://github.com/marcpoppleton/gradle-changelog-plugin.git'
                    developerConnection 'scm:git:https://github.com/marcpoppleton/gradle-changelog-plugin.git'
                    url 'https://github.com/marcpoppleton/gradle-changelog-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'marcpoppleton'
                        name 'Marc Poppleton'
                        email 'marc.poppleton@gmail.com'
                    }
                }
            }
        }
    }
}


changelog {
    file "CHANGELOG.md"
    versionNum "v$project.version"
    versionText "Electric Lama"
    repoUrl "https://github.com/marcpoppleton/gradle-changelog-plugin"
    match "^fix|^feat|^fix|^perf|^refactor|BREAKING"

//    from "0.1.0"
    //to "HEAD"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}