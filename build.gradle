buildscript {
    repositories {
        mavenLocal()
    }
}

plugins {
    id 'java-gradle-plugin'
    id  'maven'
    id 'signing'
    id 'groovy'
    id 'maven-publish'
    // Comment out the following line prior to first build, then run 'publishToMavenLocal' task,
    // then uncomment this line. Also see the 'changelog' task, below.
    id 'com.geoffgranum.gradleconventionalplugin' version '0.3.0'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.geoffgranum.gradleconventionalplugin'
            implementationClass = 'org.gradle.api.plugins.changelog.GitChangelogPlugin'
        }
    }
}

group = 'com.geoffgranum'
version = '0.3.0'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.6.1"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Conventional Changelog For Gradle',
                'Implementation-Version': project.version,
                'Built-By': 'Geoff Granum',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Changelog plugin')
                root.appendNode('description', 'Gradle plugin for generating changelog from commit logs.\n Forked from Andres Araujos plugin')
                root.appendNode('inceptionYear', '2015')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def dev = developers.appendNode('developer')
                dev.appendNode('id', 'geoffgranum')
                dev.appendNode('name', 'Geoff Granum')
                dev.appendNode('email', 'Geoff.granum@gmail.com')
            }
        }
    }
}

// Comment out the following task prior to first build, then run 'publishToMavenLocal' task, then uncomment.
changelog {
    file "CHANGELOG.md"
    versionNum "v$project.version"
    versionText "Broken Narwhal"
    repoUrl "https://github.com/ggranum/gradle-changelog-plugin"
    match "^fix|^feat|^fix|^perf|^refactor|BREAKING"

//    from "0.1.0"
    //to "HEAD"
}
